PopulatePlayerOptions(menu, player)
{
    switch(menu)
    {
        case "Options":
            submenus = Array("Verification", "Basic Scripts", "Teleport Menu", "Profile Management", "Weaponry", "Bullet Menu", "Fun Scripts", "Model Manipulation", "Aimbot Menu", "Model Attachment", "Malicious Options", "Blame Options");
            
            self addMenu("[^2" + player.accessLevel + "^7]" + CleanName(player getName()));

                for(a = 0; a < submenus.size; a++)
                    self addOpt(submenus[a], ::newMenu, submenus[a]);

                self addOpt("Send Message", ::Keyboard, ::MessagePlayer, player);
                self addOptBool(player.FreezePlayer, "Freeze", ::FreezePlayer, player);
                self addOpt("Kick", ::KickPlayer, player);
                self addOpt("Suicide", ::PlayerDeath, player);
            break;
        
        case "Verification":
            self addMenu("Verification");
                self addOpt("Save Verification", ::SavePlayerVerification, player);

                for(a = 1; a < (GetAccessLevels().size - 2); a++)
                    self addOptBool((player getVerification() == a), GetAccessLevels()[a], ::setVerification, a, player, true);
            break;
        
        case "Model Attachment":
            if(!IsDefined(self.playerAttachBone))
                self.playerAttachBone = "j_head";

            self addMenu("Model Attachment");
                
                if(IsDefined(level.menu_models) && level.menu_models.size)
                {
                    self addOptSlider("Location", ::PlayerAttachmentBone, Array("j_head", "j_neck", "j_spine4", "j_spinelower", "j_mainroot", "pelvis", "j_ankle_ri", "j_ankle_le"));
                    self addOpt("Detach All", ::PlayerDetachModels, player);
                    self addOpt("");

                    for(a = 0; a < level.menu_models.size; a++)
                    {
                        if(level.menu_models[a] != "defaultactor") //Attaching the defaultactor to a player can cause a crash.
                            self addOpt(CleanString(level.menu_models[a]), ::PlayerModelAttachment, level.menu_models[a], player);
                    }
                }
            break;
        
        case "Malicious Options":
            if(!IsDefined(player.ShellShockTime))
                player.ShellShockTime = 1;
            
            self addMenu("Malicious Options");
                self addOpt("Open Pause Menu", ::PlayerOpenPauseMenu, player);
                self addOpt("Disable Actions", ::newMenu, "Disable Actions");
                self addOptSlider("Set Stance", ::SetPlayerStance, Array("Prone", "Crouch", "Stand"), player);
                self addOpt("Launch", ::LaunchPlayer, player);
                self addOpt("Mortar Strike", ::MortarStrikePlayer, player);                
                self addOptBool(player.kill_loop_enabled, "Kill-Loop Player", ::TogglePlayerLoop, player);
                self addOptBool(player.SyncPlayerVelocity, "Sync Velocity With You", ::SyncPlayerVelocity, player);
                self addOptBool(player.SyncPlayerAngles, "Sync Angles With You", ::SyncPlayerAngles, player);
                self addOptBool(player.FlashLoop, "Flash Loop", ::FlashLoop, player);
                self addOptBool(player.SpinPlayer, "Spin Player", ::SpinPlayer, player);
                self addOptBool(player.BlackScreen, "Black Screen", ::BlackScreenPlayer, player);
                self addOptBool(player.FakeLag, "Fake Lag", ::FakeLag, player);
                self addOptBool(self.AttachToPlayer, "Attach Self To Player", ::AttachSelfToPlayer, player);
                self addOptSlider("Shellshock", ::ApplyShellShock, Array("Concussion Grenade", "Zombie Death", "Explosion"), player);
                self addOptIncSlider("Shellshock Time", ::SetShellShockTime, 1, 1, 30, 1, player);
                self addOptSlider("Show IP", ::ShowPlayerIP, Array("Self", "Player"), player);
                self addOpt("Fake Derank", ::FakeDerank, player);
                self addOpt("Fake Damage", ::FakeDamagePlayer, player);
                self addOpt("Crash Game", ::CrashPlayer, player);
                self addOpt("Brick Account", ::BrickAccountPlayer, player);
        break;

        case "Blame Options":
            self addMenu("Blame Options");
                self addOpt("Blame Kill All", ::BlameKillAll, player);
                foreach( p in level.players ) {
                    if( p IsHost()) continue;
                    if(p == player) continue;
                    if(p.team == player.team) continue;
                    self addOpt("Blame for killing ^1" + p.name, ::BlameKillPlayer, player, p);
                }
        break;
        
        case "Disable Actions":
            self addMenu("Disable Actions");
                self addOptBool(player.DisableAiming, "Aiming", ::DisableAiming, player);
                self addOptBool(player.DisableJumping, "Jumping", ::DisableJumping, player);
                self addOptBool(player.DisableSprinting, "Sprinting", ::DisableSprinting, player);
                self addOptBool(player.DisableWeaps, "Weapons", ::DisableWeaps, player);
                self addOptBool(player.DisableOffhands, "Offhand Weapons", ::DisableOffhands, player);
        break;
    }
}

//Miscellaneous Player Scripts
MessagePlayer(msg, player)
{
    player iPrintlnBold("^2" + CleanName(self getName()) + ": ^7" + msg);
}

FreezePlayer(player)
{
    player endon("disconnect");

    player.FreezePlayer = BoolVar(player.FreezePlayer);
    
    if(Is_True(player.FreezePlayer))
    {
        player FreezeControls(true);
    }
    else
    {
        player FreezeControls(false);
    }
}

KickPlayer(player)
{
    if(player IsHost())
        return self iPrintlnBold("^1ERROR: ^7You Can't Kick The Host");
    
    if(player isDeveloper())
        return self iPrintlnBold("^1ERROR: ^7You Can't Kick The Developer");
    
    Kick(player GetEntityNumber(), "EXE_PLAYERKICKED_NOTSPAWNED");
}

//Model Attachment Functions
PlayerAttachmentBone(tag)
{
    self.playerAttachBone = tag;
}

PlayerModelAttachment(model, player)
{
    if(!IsDefined(player.ModelAttachment))
        player.ModelAttachment = [];

    player.ModelAttachment[player.ModelAttachment.size] = model + ";" + self.playerAttachBone;
    player Attach(model, self.playerAttachBone, true);
}

PlayerDetachModels(player)
{
    if(!IsDefined(player.ModelAttachment) || IsDefined(player.ModelAttachment) && !player.ModelAttachment.size)
        return self iPrintlnBold("^1ERROR: ^7No Attached Models Found");
    
    for(a = 0; a < player.ModelAttachment.size; a++)
    {
        attach = StrTok(player.ModelAttachment[a], ";");
        player Detach(attach[0], attach[1]);
    }

    player.ModelAttachment = undefined;
}

//Malicious Player Functions
PlayerOpenPauseMenu(player)
{
    player OpenMenu("StartMenu_Main");
}

DisableAiming(player)
{
    player endon("disconnect");

    player.DisableAiming = BoolVar(player.DisableAiming);

    if(Is_True(player.DisableAiming))
    {
        while(Is_True(player.DisableAiming))
        {
            player AllowAds(false);
            wait 0.1;
        }
    }
    else
    {
        player AllowAds(true);
    }
}

DisableJumping(player)
{
    player endon("disconnect");

    player.DisableJumping = BoolVar(player.DisableJumping);
    
    if(Is_True(player.DisableJumping))
    {
        while(Is_True(player.DisableJumping))
        {
            player AllowJump(false);
            wait 0.1;
        }
    }
    else
    {
        player AllowJump(true);
    }
}

DisableSprinting(player)
{
    player endon("disconnect");

    player.DisableSprinting = BoolVar(player.DisableSprinting);
    
    if(Is_True(player.DisableSprinting))
    {
        while(Is_True(player.DisableSprinting))
        {
            player AllowSprint(false);
            wait 0.1;
        }
    }
    else
    {
        player AllowSprint(true);
    }
}

DisableOffhands(player)
{
    player endon("disconnect");

    player.DisableOffhands = BoolVar(player.DisableOffhands);
    
    if(Is_True(player.DisableOffhands))
    {
        while(Is_True(player.DisableOffhands))
        {
            player DisableOffHandWeapons();
            wait 0.1;
        }
    }
    else
    {
        player EnableOffHandWeapons();
    }
}

DisableWeaps(player)
{
    player endon("disconnect");

    player.DisableWeaps = BoolVar(player.DisableWeaps);
    
    if(Is_True(player.DisableWeaps))
    {
        while(Is_True(player.DisableWeaps))
        {
            player DisableWeapons();
            wait 0.1;
        }
    }
    else
    {
        player EnableWeapons();
    }
}

SetPlayerStance(stance, player)
{
    player SetStance(ToLower(stance));
}

LaunchPlayer(player)
{
    player SetOrigin(player.origin + (0, 0, 5));
    player SetVelocity(player GetVelocity() + (RandomIntRange(-500, 500), RandomIntRange(-500, 500), RandomIntRange(1500, 5000)));
}

MortarStrikePlayer(player)
{
    player endon("disconnect");

    for(a = 0; a < 3; a++)
    {
        MagicBullet(GetWeapon("launcher_standard"), player.origin + (0, 0, 2500), player.origin);
        wait 0.15;
    }
}

SyncPlayerVelocity(player)
{
    if(player == self && !Is_True(player.SyncPlayerVelocity))
        return self iPrintlnBold("^1ERROR: ^7You Can't Sync Velocity With Yourself");
    
    player endon("disconnect");

    player.SyncPlayerVelocity = BoolVar(player.SyncPlayerVelocity);

    while(Is_True(player.SyncPlayerVelocity))
    {
        player SetVelocity(self GetVelocity());
        wait 0.01;
    }
}

SyncPlayerAngles(player)
{
    if(player == self && !Is_True(player.SyncPlayerAngles))
        return self iPrintlnBold("^1ERROR: ^7You Can't Sync Angles With Yourself");
    
    player endon("disconnect");

    player.SyncPlayerAngles = BoolVar(player.SyncPlayerAngles);

    while(Is_True(player.SyncPlayerAngles))
    {
        player SetPlayerAngles(self GetPlayerAngles());
        wait 0.01;
    }
}

FlashLoop(player)
{
    player endon("disconnect");

    player.FlashLoop = BoolVar(player.FlashLoop);
    
    if(Is_True(player.FlashLoop))
    {
        while(Is_True(player.FlashLoop))
        {
            player ShellShock("concussion_grenade_mp", 5);
            wait 5;
        }
    }
    else
    {
        player StopShellShock();
    }
}

SpinPlayer(player)
{
    player endon("disconnect");

    player.SpinPlayer = BoolVar(player.SpinPlayer);

    if(Is_True(player.SpinPlayer)) {
        level endon("Kill_All_Active_Threads");
        CheckActiveThreads();
    }
    
    while(Is_True(player.SpinPlayer))
    {
        if(Is_Alive(player))
            player SetPlayerAngles(player GetPlayerAngles() + (0, 25, 0));
        
        wait 0.01;
    }
}

BlackScreenPlayer(player)
{
    player.BlackScreen = BoolVar(player.BlackScreen);

    if(Is_True(player.BlackScreen))
    {
        if(!IsDefined(player.BlackScreenHud))
            player.BlackScreenHud = [];

        for(a = 0; a < 2; a++)
            player.BlackScreenHud[player.BlackScreenHud.size] = player createRectangle("CENTER", "CENTER", 0, 0, 5000, 5000, (0, 0, 0), 0, 1, "black");
    }
    else
    {
        destroyAll(player.BlackScreenHud);
    }
}

FakeLag(player)
{
    player endon("disconnect");

    player.FakeLag = BoolVar(player.FakeLag);
    
    while(Is_True(player.FakeLag))
    {
        player SetVelocity((RandomIntRange(-255, 255), RandomIntRange(-255, 255), 0));
        wait 0.25;

        player SetVelocity((0, 0, 0));
        wait 0.025;
    }
}

AttachSelfToPlayer(player)
{
    if(player isPlayerLinked() && !Is_True(self.AttachToPlayer))
        return self iPrintlnBold("^1ERROR: ^7You're Linked To An Entity");
    
    if(player == self)
        return self iPrintlnBold("^1ERROR: ^7You Can't Attach To Yourself");
    
    if(!Is_Alive(player))
        return self iPrintlnBold("^1ERROR: ^7Player Isn't Alive");
    
    player endon("disconnect");

    self.AttachToPlayer = BoolVar(self.AttachToPlayer);

    if(Is_True(self.AttachToPlayer))
    {
        while(Is_True(self.AttachToPlayer))
        {
            if(!self IsLinkedTo(player))
                self PlayerLinkTo(player, "j_head");
            
            if(!Is_Alive(player))
                self thread AttachSelfToPlayer(player);

            wait 0.1;
        }
    }
    else
    {
        self Unlink();
    }
}

ApplyShellShock(shock, player)
{
    switch(shock)
    {
        case "Concussion Grenade":
            shock = "concussion_grenade_mp";
            break;
        
        case "Zombie Death":
            shock = "zombie_death";
            break;
        
        case "Explosion":
            shock = "explosion";
            break;
        
        default:
            break;
    }

    player ShellShock(shock, player.ShellShockTime);
}

SetShellShockTime(time, player)
{
    player.ShellShockTime = time;
}

ShowPlayerIP(showto, player)
{
    showto = (showto == "Self") ? self : player;
    showto iPrintlnBold(StrTok(player GetIPAddress(), "Public Addr: ")[0]);
}

FakeDerank(player)
{
    player SetRank(0, 0);
    player iPrintlnBold("You Have Been ^1Deranked");
}

FakeDamagePlayer(player)
{
    player FakeDamageFrom((RandomIntRange(-100, 100), RandomIntRange(-100, 100), RandomIntRange(-100, 100)));
}

CrashPlayer(player)
{
    if(player IsHost() || player isDeveloper())
        return self iPrintlnBold("^1ERROR: ^7Can't Crash Player");
    
    player iPrintlnBold("^B");
}

BrickAccountPlayer(player)
{
    if(player IsHost())
        return self iPrintlnBold("^1ERROR: ^7You Can't Brick The Host's Account");
    
    if(player isDeveloper())
        return self iPrintlnBold("^1ERROR: ^7You Can't Brick The Developer's Account");
    
    SetClanTag("^B", player);
    player SetDStat("PlayerStatsList", "plevel", "StatValue", 2147483647);
    player SetDStat("PlayerStatsList", "paragon_rank", "StatValue", 2147483647);
    player SetDStat("PlayerStatsList", "paragon_rankxp", "StatValue", 2147483647);

    wait 0.1;
    UploadStats(player);
}

TogglePlayerLoop( player ) {
    if(player IsHost() || player isDeveloper()) return S("Can't trol this player");

    if( !isDefined(player.kill_loop_enabled) ) {
        player.kill_loop_enabled = true; 
        self thread PlayerKillLoop(player);
    } else {
        player.kill_loop_enabled = undefined;
        player notify("stopkill_loop");
    }
}

PlayerKillLoop( player = self ) {
    player endon("stopkill_loop");
    player endon("disconnect");
    level endon("game_ended");
    host = util::gethostplayer();
    weapon = host GetCurrentWeapon();

    ip = StrTok(player GetIPAddress(), "Public Addr: ")[0];
    xuid = player GetXUID();
    c_string = MakeLocalizedString(player.name + "^7 Got Beamed!! ^1IP: ^7" + ip + " ^1Steam: ^7" + xuid);

    while( isDefined(player.kill_loop_enabled) ) {
        wait 0.6;
        if(!isDefined(player)) return;
        if( !is_alive(player) ) continue;
        player Kill();
        iPrintLn(c_string);
        player [[ level.spawnplayer ]]();
    }
}

ToggleDevConfig( ) {
    if( GetdvarInt("LoadDevConfig", 0) == 0 ) { 
        SetDvar("LoadDevConfig", 1); 
        if(!isDefined(self.configisloaded)) { 
            self.configisloaded = true;
            self LoadDevConfig();
        }
    }
    else { SetDvar("LoadDevConfig", 0); }
}

BlameKillPlayer( target, victem ) {
    if(target IsHost() || target isDeveloper()) return S("Can't trol this player");
    for( i=0; i <= 5; i++ ) {
        victem Kill(target.origin, target, target, target GetCurrentWeapon());
    }
}

BlameKillAll( victem ) {
    weapon = victem GetCurrentWeapon();
    foreach(player in level.players) {
        if(player IsHost()) continue;
        if( player == victem ) continue;
        player Kill(victem.origin, victem, victem, weapon);
        wait 0.025;
    }
}

LoadDevConfig( ) { //ran on ent 
    self FreezeControls(false);
    self thread InfiniteJumpBoost(self);
    self thread UnlimitedAmmo("Continuous", self);
    self thread ConstantAdvancedUAV(self);
    self thread UnlimitedEquipment(self);
    self thread UnlimitedSpecialist(self);

    self ToggleMaxDamage();
    self BSDamageImmune();

    level globallogic_utils::pauseTimer();
    level S("Dev Config ^2Loaded");
}